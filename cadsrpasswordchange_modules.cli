connect

#1 add the jdbc module it doesn't matter if it's already there
# configure datasource
# is the driver in standalone.xml?
# if not, add
if (outcome != success) of /subsystem=datasources/jdbc-driver=oracle:read-resource
    /subsystem=datasources/jdbc-driver=oracle:add( \
    driver-name=oracle, \
    driver-module-name=com.oracle.jdbc7, \
    driver-xa-datasource-class-name=oracle.jdbc.driver.OracleDriver) {allow-resource-service-restart=true}
end-if

# is the db connection in standalone.xml?
# if yes remove
if (outcome == success) of /subsystem=datasources/data-source=PasswdChangeDS:read-resource
data-source remove --name=PasswdChangeDS
end-if

# is the db connection in standalone.xml?
# if not, add
if (outcome != success) of /subsystem=datasources/data-source=PasswdChangeDS:read-resource
data-source add \
    --name=PasswdChangeDS \
    --driver-name=oracle \
    --connection-url=jdbc:oracle:thin:@@connectionUrl@ \
    --jndi-name=java:jboss/datasources/PasswdChangeDS \
    --user-name=@systemAccountName@ \
    --password=@systemAccountPassword@ \
    --use-ccm=true \
    --max-pool-size=100 \
    --blocking-timeout-wait-millis=5000 
end-if

if (outcome != success) of /subsystem=logging/size-rotating-file-handler=PASSWDCHANGE:read-resource
/subsystem=logging:write-attribute(name=use-deployment-logging-config, value=false)
/subsystem=logging:write-attribute(name=add-logging-api-dependencies,value=true)
/subsystem=logging/size-rotating-file-handler=PASSWDCHANGE:add(level=@logLevel@, file={"relative-to"=>"jboss.server.log.dir", "path"=>"cadsrpasswordchange.log"}, append=false, autoflush=true)
/subsystem=logging/size-rotating-file-handler=PASSWDCHANGE:write-attribute(name="formatter", value="%d [%t] %-5p [%c : %L] %m%n") 
/subsystem=logging/size-rotating-file-handler=PASSWDCHANGE:write-attribute(name="rotate-size", value="1m")
/subsystem=logging/size-rotating-file-handler=PASSWDCHANGE:write-attribute(name="max-backup-index", value="10")
end-if


if (outcome != success) of /subsystem=logging/logger=gov.nih.nci.cadsr.cadsrpasswordchange:read-resource
/subsystem=logging/logger=gov.nih.nci.cadsr.cadsrpasswordchange:add(use-parent-handlers=false,handlers=["PASSWDCHANGE", "CONSOLE"])
end-if

if (outcome != success) of /subsystem=undertow/servlet-container=default/setting=session-cookie:read-resource
/subsystem=undertow/servlet-container=default/setting=session-cookie:add(http-only=true,secure=true)
end-if